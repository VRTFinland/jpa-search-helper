plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.springframework.boot' version '2.7.11'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'app.tozzi'
version = '1.0.0'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

tasks.bootJar {enabled = false}
tasks.jar {enabled = true}

dependencies {
    // JPA
    implementation 'jakarta.persistence:jakarta.persistence-api:2.2.3'

    // Spring
    implementation 'org.springframework.data:spring-data-jpa:2.7.11'
    implementation 'org.springframework:spring-beans'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testImplementation 'org.projectlombok:lombok:1.18.26'

    // Utils
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // Test
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

publishing {
    publications {
        maven(MavenPublication) {
            group = 'app.tozzi'
            artifactId = 'jpa-search-helper'
            version = "1.0.0"
            from components.java
            pom {
                name = 'JPA Search Helper'
                description = 'Helper library for building advanced and dynamic queries using JPA in Spring'
                url = 'https://www.tozzi.app'

                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }

                developers {
                    developer {
                        id = 'biagio.tozzi'
                        name = 'Biagio Placido Tozzi'
                        email = 'biagio.tozzi@gmail.com'
                        url = 'https://www.tozzi.app'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com:biagioT/jpa-search-helper.git'
                    developerConnection = 'scm:git:ssh://github.com:biagioT/jpa-search-helper.git'
                    url = 'https://github.com/biagioT/jpa-search-helper/tree/main'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/VRTfinland/jpa-search-helper"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

javadoc {
    options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.main.allSource
}
